include "inc/cmdsys.plh"
include "inc/conio.plh"

byte[] initstr
byte = " (        )\n"
byte = " )\\ )  ( /(  (\n"
byte = "(()/(  )\\()) )\\ )       (   (\n"
byte = " /(_))((_)\\ (()/(       )\\  )\\\n"
byte = "(_))    ((_) /(_))_  _ ((_)((_)\n"
byte = "| _ \\  / _ \\(_)) __|| | | || __|\n"
byte = "|   / | (_) | | (_ || |_| || _|\n"
byte = "|_|_\\  \\___/   \\___| \\___/ |___|\n"
byte = "\n"
byte = "       By Resman\n"
byte = "       Artwork by Seth Sternberger\n"
byte = ""
word titlestr = @initstr

//
// Machine specific routines
//

export word rnd, getkb, tone

const ENV_REG = $FFDF

const SPEAKER = $C030

const a2rndnum = $4E // ZP location of RND
const a2rndl   = $4E
const a2rndh   = $4F

word a3rndnum = 12345

def a3rnd
  a3rndnum = (a3rndnum << 1) + a3rndnum + 123
  return *a3rndnum & $7FFF
end

def a2rnd
  *a2rndnum = (*a2rndnum << 1) + *a2rndnum + 123
  return *a2rndnum & $7FFF
end

def a2getkb
  return getc()
end

def a2tone(duration, delay)
  byte i

  while duration
    ^SPEAKER
    for i = 0 to delay
    next
    duration--
  loop
  return 0
end

def a3tone(duration, pitch)
  byte env

  env = ^ENV_REG
  ^ENV_REG = env | $C0
  a2tone(duration, pitch)
  ^ENV_REG = env
  return 0
end

//
// Apple /// console routines
//

def a3getkb
  while not conio:keypressed()
    a3rndnum = a3rndnum + 123
  loop
  return getc()
end

//
// Set machine specific routines
//

when MACHID & $C8
  is $08 // Apple 1
    puts("APPLE 1 NOT SUPPORTED.")
    return -1
  is $C0 // Apple ///
    rnd     = @a3rnd
    getkb   = @a3getkb
    tone    = @a3tone
    break
  otherwise // Apple ][
    rnd     = @a2rnd
    getkb   = @a2getkb
    tone    = @a2tone
wend

//
// Print title page
//

conio:home()
while ^titlestr
  puts(titlestr)
  titlestr = titlestr + ^titlestr + 1
loop

done
