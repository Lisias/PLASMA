include "inc/cmdsys.plh"
include "inc/hgrlib.plh"
include "inc/sprite.plh"
include "inc/lines.plh"
sysflags reshgr1|reshgr2 // Reserve HGR page 1 and 2

byte ball = $88, $83, $33, $38, $88
byte      = $88, $33, $11, $33, $88
byte      = $83, $31, $11, $13, $38
byte      = $33, $11, $11, $11, $33
byte      = $33, $11, $11, $11, $33
byte      = $33, $11, $11, $11, $33
byte      = $33, $11, $11, $11, $33
byte      = $83, $31, $11, $13, $38
byte      = $88, $33, $11, $33, $88
byte      = $88, $83, $33, $38, $88
var sprtBall

def testline#0
  var i

  setlinespans(@hgrHlin, @hgrVlin)
  hgrColor(7)
  for i = 0 to 191 step 8
    linespans(0, i, 279, 191 - i)
  next
  for i = 0 to 279 step 9
    linespans(i, 0, 279 - i, 191)
  next
end
def testblt(sprtnum)#0
  var[16] i, j, ii, jj
  byte k

  sprtnum = sprtnum - 1
  i[0] = 100
  ii[0] = 1
  j[0] = 80
  jj[0] = -1
  if sprtnum
    for k = 1 to sprtnum
      i[k] = ((i[k - 1] ^ 37) + 97) % 270
      ii[k] = -ii[k - 1]
      j[k] = ((j[k - 1] ^ 29) + 53) % 180
      jj[k] = -jj[k - 1]
    next
  fin
  while ^$C000 < 128
    for k = 0 to sprtnum
      i[k] = i[k] + ii[k]
      if i[k] < -10 or i[k] > 280
        ii[k] = -ii[k]
      fin
      j[k] = j[k] + jj[k]
      if j[k] < -10 or j[k] > 192
        jj[k] = -jj[k]
      fin
      spritePosIndex(i[k], j[k], k)
    next
    spriteDrawList()
    spriteDrawBuf(hgrSwap())
  loop
end

hgrMode
spriteDrawBuf(hgrDrawBuf(0)) // So we can see the compile process
sprtBall = spriteCompile(10, 10, @ball)
hgrColor(0)
hgrClear()
testline
memcpy($4000, $2000, $2000) // Copy to both buffers
spriteDrawBuf(hgrDrawBuf(1))
spriteAdd(0, sprtBall)
spriteAdd(1, spriteDup(sprtBall))
spriteAdd(2, spriteDup(sprtBall))
spriteAdd(3, spriteDup(sprtBall))
spriteAdd(4, spriteDup(sprtBall))
spriteAdd(5, spriteDup(sprtBall))
spriteAdd(6, spriteDup(sprtBall))
spriteAdd(7, spriteDup(sprtBall))
spriteAdd(8, spriteDup(sprtBall))
spriteAdd(9, spriteDup(sprtBall))
spriteAdd(10, spriteDup(sprtBall))
spriteAdd(11, spriteDup(sprtBall))
spriteAdd(12, spriteDup(sprtBall))
spriteAdd(13, spriteDup(sprtBall))
spriteAdd(14, spriteDup(sprtBall))
spriteAdd(15, spriteDup(sprtBall))
testblt(16)
getc
txtMode
done
