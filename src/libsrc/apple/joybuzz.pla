asm asmdefs
        !SOURCE    "vmsrc/plvmzp.inc"
SPEAKER	=       $C030
GCSTRB  =       $C070
GC0     =       $C064
GC1     =       $C065
GCPB1	=       $C061
GCPB2	=       $C062
GCMAX   =       64          ; MAX VALUE FOR GAME CONTROLLER
end
//
// Read both game controllers in parallel
// 
export asm joypos(buzz)#3
        DEX
        DEX
        LDA     #$00
        STA     ESTKH,X
        STA     ESTKH+1,X
        STA     ESTKH+2,X
        LDA     #GCMAX
        STA     TMPL
        STA     ESTKL,X
        STA     ESTKL+1,X
        LDY     ESTKL+2,X   ; BUZZ TONE        
        BIT     GCSTRB
JOYZZLP DEY
        BEQ     +
        NOP                 ; TIMING
        NOP
        NOP
        NOP
        BNE     ++
+       BIT     SPEAKER
        LDY     ESTKL+2,X   ; BUZZ TONE        
++      LDA     #$FF
        ASL     GC0
        ADC     ESTKL,X
        ASL     GC1
        STA     ESTKL,X
        LDA     #$FF
        ADC     ESTKL+1,X
        STA     ESTKL+1,X
        DEC     TMPL
        BNE     JOYZZLP
        ASL     GCPB1       ; READ GC BUTTONS
        LDA     GCPB2
        ROR
        AND     #$C0
        STA     ESTKL+2,X
        RTS
end
done
