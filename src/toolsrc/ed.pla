//=====================================
//
//              Text Editor
//
//=====================================

include "inc/cmdsys.plh"
include "inc/args.plh"
include "inc/fileio.plh"
sysflags nojitc // Keep JITC from compiling and pausing while editing
//
// Hardware constants
//
const csw           = $0036
const pushbttn1     = $C061
const pushbttn2     = $C062
const pushbttn3     = $C063
const keyboard      = $C000
const keystrobe     = $C010
const inputln       = $01FF
//
// ASCII key values
//
const keyenter      = $8D
const keyspace      = $A0
const keyarrowup    = $8B
const keyarrowdown  = $8A
const keyarrowleft  = $88
const keyarrowright = $95
const keyescape     = $9B
const keyctrla      = $81
const keyctrlb      = $82
const keyctrlc      = $83
const keyctrld      = $84
const keyctrle      = $85
const keyctrlf      = $86
const keyctrlg      = $87
const keyctrli      = $89
const keyctrlk      = $8B
const keyctrll      = $8C
const keyctrln      = $8E
const keyctrlo      = $8F
const keyctrlp      = $90
const keyctrlq      = $91
const keyctrlr      = $92
const keyctrls      = $93
const keyctrlt      = $94
const keyctrlu      = $95
const keyctrlv      = $96
const keyctrlw      = $97
const keyctrlx      = $98
const keyctrly      = $99
const keyctrlz      = $9A
const keydelete     = $FF
//
// Data and text buffer constants
//
const MAXLINES      = 999
const MAXLINESSIZE  = MAXLINES+24
const MAXCLIPLINES  = 256
const MAXLNLEN      = 79
const MAXSTRPLSIZE  = $8000
//const STRPLMAPSIZE  = 224 // $E0 = 28K is memory@16 bytes per bit map, 128 bytes per 8 bit map, 1K bytes per 8 byte map
const pgjmp         = 16
const changed       = 1
const insmode       = 2
const showcurs      = 4
const uppercase     = 8
const selection     = 16
const shiftlock     = 128
//
// Text screen row address array
//
const scrnwidth   = 35
word txtscrn      = $0404,$0484,$0504,$0584,$0604,$0684,$0704,$0784
word              = $042C,$04AC,$052C,$05AC,$062C,$06AC,$072C,$07AC
word              = $0454,$04D4,$0554,$05D4,$0654,$06D4,$0754,$07D4
//
// Editor variables
//
byte nullstr       = ""
byte[80] findstr   = ""
byte[64] filename  = "UNTITLED"
byte exit          = FALSE
byte flags         = 0
byte flash         = 0
word numlines      = 0
word numcliplines  = 0
word arg
word strplsize     = MAXSTRPLSIZE
word strpool, strplmapsize, txtlinbuf, cliplinbuf, strpoolmap
byte cursx, cursy, scrnleft, curscol, underchr, curschr
word keyin, cursrow, selrow, scrntop, cursptr
byte a3echo         = $80
byte a3noecho       = $00
//
// Predeclared functions
//
predef cmdmode#0
//
// Utility functions
//
// Defines for ASM routines
//
asm equates
    !SOURCE "vmsrc/plvmzp.inc"
end
asm sethibit(strptr)#0
        LDA     ESTKL,X
        STA     SRCL
        LDA     ESTKH,X
        STA     SRCH
        INX
        LDY     #$00
        LDA     (SRC),Y
        BEQ     +
        TAY
STHILP  LDA     (SRC),Y
        ORA     #$80
        STA     (SRC),Y
        DEY
        BNE     STHILP
+       RTS
end
asm lncpy(dststr, srcstr)#0
        LDA     ESTKL,X
        STA     SRCL
        LDA     ESTKH,X
        STA     SRCH
        INX
        LDA     ESTKL,X
        STA     DSTL
        LDA     ESTKH,X
        STA     DSTH
        INX
        LDY     #$00
        LDA     (SRC),Y
        TAY
        LDA     #$00
        INY
        STA     (DST),Y
        DEY
        BEQ     ++
CPLNLP  LDA     (SRC),Y
        CMP     #$20
        BCS     +
        ADC     #$60
+       AND     #$7F
        STA     (DST),Y
        DEY
        BNE     CPLNLP
        LDA     (SRC),Y
++      STA     (DST),Y
        RTS
end
asm lnupcpy(dststr, srcstr)#0
        LDA     ESTKL,X
        STA     SRCL
        LDA     ESTKH,X
        STA     SRCH
        INX
        LDA     ESTKL,X
        STA     DSTL
        LDA     ESTKH,X
        STA     DSTH
        INX
        LDY     #$00
        LDA     (SRC),Y
        TAY
        LDA     #$00
        INY
        STA     (DST),Y
        DEY
        BEQ     +++
CPUPLP  LDA     (SRC),Y
        CMP     #$20
        BCS     +
        ADC     #$60
+       AND     #$7F
        CMP     #$7B
        BCS     ++
        CMP     #$61
        BCC     ++
        SEC
        SBC     #$20
++      STA     (DST),Y
        DEY
        BNE     CPUPLP
        LDA     (SRC),Y
+++     STA     (DST),Y
        RTS
end
def bell#0
    putc($07)
end
//
// Memory management routines
//
def sizemask(size)
    if size <= 16
        return $01
    elsif size <= 32
        return $03
    elsif size <= 48
        return $07
    elsif size <= 64
        return $0F
    elsif size <= 80
        return $1F
    fin
    return 0
end
def strpoolalloc(size)
    byte szmask
    word mapmask, addr, i

    szmask = sizemask(size)
    for i = strplmapsize - 1 downto 0
        if ^(strpoolmap + i) <> $FF
            mapmask = szmask
            repeat
                if ^(strpoolmap + i) & mapmask
                    mapmask = mapmask << 1
                else
                    ^(strpoolmap + i) = ^(strpoolmap + i) | mapmask
                    addr = (i << 7) + strpool
                    while !(mapmask & 1)
                        addr = addr + 16
                        mapmask = mapmask >> 1
                    loop
                    return addr
                fin
            until mapmask & $100
        fin
    next
    bell()
    puts("OUT OF MEMORY!")
    return 0
end
def striplead(strptr, chr)#0
    byte striplen
    
    for striplen = 1 to ^strptr
        if ^(strptr + striplen) <> chr
            break
        fin
    next
    if striplen > 1
        ^strptr = ^strptr - (striplen - 1)
        memcpy(strptr + 1, strptr + striplen, ^strptr)
    fin
end
def striptail(strptr, chr)#0
    byte strlen

    for strlen = ^strptr downto 1
        if ^(strptr + strlen) <> $8D and ^(strptr + strlen) <> chr
            break
        fin
    next
    ^strptr = strlen
end
def strstripcpy(dststr, srcstr)#0
    memcpy(dststr, srcstr, ^srcstr + 1)
    striptail(dststr, $80 | ' ')
end
def delstr(strptr)#0
    byte mask, ofst

    if strptr and strptr <> @nullstr
        mask = sizemask(^strptr + 1)
        ofst = (strptr - strpool) >> 4
        mask = mask << (ofst & $07)
        ofst = ofst >> 3
        ^(strpoolmap + ofst) = ^(strpoolmap + ofst) & ~mask
    fin
end
def newstr(strptr)
    byte strlen
    word newptr

    strlen = ^strptr
    while ^(strptr + strlen) == $8D or ^(strptr + strlen) == $A0
        strlen--
    loop
    if strlen == 0
        return @nullstr
    fin
    newptr = strpoolalloc(strlen + 1)
    if newptr
        memcpy(newptr, strptr, strlen + 1)
        ^newptr = strlen
        return newptr
    fin
    return @nullstr
end
def inittxtbuf#0
    word i

    if not strpool
        txtlinbuf  = heapalloc(MAXLINESSIZE*2)
        cliplinbuf = heapalloc(MAXCLIPLINES*2)
        while isult(heapavail, strplsize)
            strplsize = strplsize - 4096
        loop
        if isult(heapavail - strplsize, 4096) // Keep at least 4096 free
            strplsize = strplsize - 4096
        fin
        strplmapsize = strplsize / 128
        strpoolmap   = heapalloc(strplmapsize)
        strpool      = heapalloc(strplsize)
        memset(txtlinbuf,  @nullstr, MAXLINESSIZE*2)
        memset(cliplinbuf, @nullstr, MAXCLIPLINES*2)
        memset(strpoolmap, 0, strplmapsize)
    else
        for i = 0 to MAXLINESSIZE-1
            if txtlinbuf=>[i] <> @nullstr
                delstr(txtlinbuf=>[i])
                txtlinbuf=>[i] = @nullstr
            fin
        next
    fin
    numlines   = 1
    cursrow    = 0
    curscol    = 0
    cursx      = 0
    cursy      = 0
    scrnleft   = 0
    scrntop    = 0
    flags      = flags & ~selection
end
//
// Case conversion/printing routines
//
def caseconv(chr)
    if flags & uppercase
        if chr & $E0 == $E0
            chr = chr - $E0
        fin
    fin
    return chr
end
def strupper(strptr)#0
    byte i, chr

    for i = ^strptr downto 1
        chr = (strptr).[i]
        if chr & $E0 == $E0
            (strptr).[i] = chr - $E0
        fin
    next
end
def strlower(strptr)#0
    byte i, chr

    for i = ^strptr downto 1
        chr = (strptr).[i]
        if chr & $E0 == $00
            (strptr).[i] = chr + $E0
        fin
    next
end
def txtupper#0
    word i, strptr

    flags = flags | uppercase
    for i = numlines - 1 downto 0
        strupper(txtlinbuf=>[i])
    next
end
def txtlower#0
    word i, strptr

    flags = flags & ~uppercase
    for i = numlines - 1 downto 0
        strlower(txtlinbuf=>[i])
    next
end
def strtonum(strptr)
    word num, i
    byte c
    
    num = 0
    for i = 1 to ^strptr
        c = ^(strptr + i) & $7F
        if c < '0' and c > '9'
            break
        fin
        num = num * 10 + c - '0'
    next
    return num
end
def nametostr(namestr, len, strptr)#0
    ^strptr = len
    memcpy(strptr + 1, namestr, len)
end
//
// File routines
//
def readtxt(filename, startline)#0
    byte txtbuf[81], refnum, i, j

    refnum = fileio:open(filename)
    if refnum
        fileio:newline(refnum, $7F, $0D)
        numlines = startline
        repeat
            txtbuf = fileio:read(refnum, @txtbuf + 1, MAXLNLEN)
            if txtbuf
                sethibit(@txtbuf)
                if flags & uppercase; strupper(@txtbuf); fin
                txtlinbuf=>[numlines] = newstr(@txtbuf)
                numlines++
            fin
            if !(numlines & $0F); putc('.'); fin
        until txtbuf == 0 or numlines == MAXLINES
        fileio:close(refnum)
        //
        // Make sure there is a blank line at the end of the buffer
        //
        if numlines < MAXLINES and txtlinbuf=>[numlines - 1] <> @nullstr
            txtlinbuf=>[numlines] = @nullstr
            numlines++
        fin
    fin
end
def writetxt(filename)#0
    byte txtbuf[81], refnum
    byte j, chr
    word i, strptr

    fileio:destroy(filename)
    fileio:create(filename, $04, $00) // full access, TXT file
    refnum = fileio:open(filename)
    if refnum == 0
        return
    fin
    //
    // Remove blank lines at end of text.
    //
    while numlines > 1 and txtlinbuf=>[numlines - 1] == @nullstr; numlines--; loop
    //
    // Write all the text line to the file.
    //
    for i = 0 to numlines - 1
        lncpy(@txtbuf, txtlinbuf=>[i])
        txtbuf++; txtbuf[txtbuf] = $0D // Add CR to end of line
        fileio:write(refnum, @txtbuf + 1, txtbuf)
        if !(i & $0F); putc('.'); fin
    next
    fileio:close(refnum)
end
//
// Screen routines
//
def clrscrn#0
    if MACHID == $F2 // Apple 3
        putc(28)
    else
        call($FC58, 0, 0, 0, 0)
    fin
end
def drawrow(row, ofst, strptr)#0
    byte numchars
    word scrnptr

    scrnptr = txtscrn[row]
    if ofst >= ^strptr
        numchars = 0
    else
        numchars = ^strptr - ofst
    fin
    if numchars >= 36
        numchars = 36
    else
        memset(scrnptr + numchars, $A0A0, 36 - numchars)
    fin
    memcpy(scrnptr, strptr + ofst + 1, numchars)
end
def drawgutter(scrnrow, ofst)#0
    byte row, hilite, ofstch, huns, tens, ones
    word scrnptr, scrnrow

    //
    // Draw line numbers and gutter hilites
    //
    ofstch = ofst ?? $80 | '<' :: $80 | ' '
    huns, tens = divmod(scrnrow + 1, 100)
    tens, ones = divmod(tens, 10)
    for row = 0 to 23
        scrnptr = txtscrn[row] - 4
        if scrnrow < numlines
            if flags & selection and (scrnrow >= selrow and scrnrow <= cursrow) or (scrnrow >= cursrow and scrnrow <= selrow)
                hilite = $00
            elsif scrnrow == cursrow
                hilite = $00
            else
                hilite = $80
            fin
            if huns
                ^scrnptr     = hilite + '0' + huns
                ^(scrnptr+1) = hilite + '0' + tens
                ^(scrnptr+2) = hilite + '0' + ones
                ones++
                if ones > 9
                    ones = 0
                    tens++
                    if tens > 9
                        tens = 0
                        huns++
                    fin
                fin
            elsif tens
                ^scrnptr     = hilite + ' '
                ^(scrnptr+1) = hilite + '0' + tens
                ^(scrnptr+2) = hilite + '0' + ones
                ones++
                if ones > 9
                    ones = 0
                    tens++
                    if tens > 9
                        tens = 0
                        huns = 1
                    fin
                fin
            elsif ones
                ^scrnptr     = hilite + ' '
                ^(scrnptr+1) = hilite + ' '
                ^(scrnptr+2) = hilite + '0' + ones
                ones++
                if ones > 9
                    ones = 0
                    tens = 1
                fin
            fin
            ^(scrnptr+3) = ofstch
        else
            *scrnptr     = $A0A0
            *(scrnptr+2) = $A0A0
        fin
        scrnrow++
    next
end
def drawscrn(toprow, ofst)#0
    byte row, numchars
    word strptr, scrnptr
    
    drawgutter(toprow, ofst)
    //
    // Draw text
    //
    if ofst
        for row = 0 to 23
            strptr   = txtlinbuf=>[toprow + row]
            scrnptr  = txtscrn[row]
            if ofst >= ^strptr
                numchars = 0
            else
                numchars = ^strptr - ofst
            fin
            if numchars >= 36
                numchars = 36
            else
                memset(scrnptr + numchars, $A0A0, 36 - numchars)
            fin
            memcpy(scrnptr, strptr + ofst + 1, numchars)
        next
    else
        for row = 0 to 23
            strptr   = txtlinbuf=>[toprow + row]
            scrnptr  = txtscrn[row]
            numchars = ^strptr
            if numchars >= 36
                numchars = 36
            else
                memset(scrnptr + numchars, $A0A0, 36 - numchars)
            fin
            memcpy(scrnptr, strptr + 1, numchars)
        next
    fin
end
def cursoff#0
    word scrnptr
    
    if flags & showcurs
        ^cursptr     = underchr
        scrnptr      = txtscrn[cursy] - 4
        *scrnptr     = *scrnptr     | $8080
        ^(scrnptr+2) = ^(scrnptr+2) | $80
        flags = flags & ~showcurs
        
    fin
end
def curson#0
    word scrnptr
    
    if !(flags & showcurs)
        cursptr  = txtscrn[cursy] + cursx
        underchr = ^cursptr
        ^cursptr = curschr
        scrnptr = txtscrn[cursy] - 4
        *scrnptr     = *scrnptr     & $7F7F
        ^(scrnptr+2) = ^(scrnptr+2) & $7F
        flags = flags | showcurs
    fin
end
def cursflash#0
    if flags & showcurs
        if flash == 0
            ^cursptr = curschr
        elsif flash == 128
            ^cursptr = underchr
        fin
        flash++
    fin
end
def redraw#0
    cursoff
    drawscrn(scrntop, scrnleft)
    curson
end
def curshome#0
    cursoff
    cursrow  = 0
    curscol  = 0
    cursx    = 0
    cursy    = 0
    scrnleft = 0
    scrntop  = 0
    drawscrn(scrntop, scrnleft)
    curson
end
def cursend#0
    cursoff
    if numlines > 23
        cursrow  = numlines - 1
        cursy    = 23
        scrntop  = cursrow - 23
    else
        cursrow  = numlines - 1
        cursy    = numlines - 1
        scrntop  = 0
    fin
    curscol  = 0
    cursx    = 0
    scrnleft = 0
    drawscrn(scrntop, scrnleft)
    curson
end
def cursup#0
    if cursrow > 0
        cursoff
        cursrow--
        if cursy > 0
            cursy--
            if flags & selection
                drawgutter(scrntop, scrnleft)
            fin
        else
            scrntop = cursrow
            drawscrn(scrntop, scrnleft)
        fin
        curson
    fin
end
def pgup#0
    byte i

    for i = 0 to pgjmp
        cursup
    next
end
def cursdown#0
    if cursrow < numlines - 1
        cursoff
        cursrow++
        if cursy < 23
            cursy++
            if flags & selection
                drawgutter(scrntop, scrnleft)
            fin
        else
            scrntop = cursrow - 23
            drawscrn(scrntop, scrnleft)
        fin
        curson
    fin
end
def pgdown#0
    byte i

    for i = 0 to pgjmp
        cursdown
    next
end
def cursleft#0
    if curscol > 0
        cursoff
        curscol--
        if cursx > 0
            cursx--
        else
            scrnleft = curscol
            drawscrn(scrntop, scrnleft)
        fin
        curson
    fin
end
def pgleft#0
    byte i

    for i = 0 to 7
        cursleft
    next
end
def cursright#0
    if curscol < MAXLNLEN
        cursoff
        curscol++
        if cursx < scrnwidth
            cursx++
        else
            scrnleft = curscol - scrnwidth
            drawscrn(scrntop, scrnleft)
        fin
        curson
    fin
end
def pgright#0
    byte i

    for i = 0 to 7
        cursright
    next
end
def curshpos(hpos)#0
    cursoff
    curscol = hpos
    if curscol > scrnwidth
        cursx    = curscol - scrnwidth
        scrnleft = scrnwidth
    else
        cursx    = curscol
        scrnleft = 0
    fin
    curson
end
def cursvpos(vpos)#0
    cursrow = vpos
    if cursrow > numlines - 1
        cursrow = numlines - 1
    fin
    if numlines > 23 and cursrow > 12
        cursy    = 12
        scrntop  = cursrow - 12
    else
        cursy    = cursrow
        scrntop  = 0
    fin
    redraw
end
//
// Find string in text
//
def findline(strptr)#1
    byte upstr[80], scan, i
    
    if ^strptr >= findstr
        lnupcpy(@upstr, strptr)
        for scan = 1 to upstr - findstr + 1
            if upstr[scan] == findstr[1]
                for i = 2 to findstr
                    if upstr[scan + i - 1] <> findstr[i]
                        break
                    fin
                next
                if i > findstr
                    curshpos(scan - 1)
                    return TRUE
                fin
            fin
        next
    fin
    return FALSE
end
def findtxt#0
    word f

    //
    // Search from current pos to end
    //
    for f = cursrow + 1 to numlines - 1
        if findline(txtlinbuf=>[f])
            cursvpos(f)
            return
        fin
    next
    //
    // Search from beginning to current pos
    //
    for f = 0 to cursrow
        if findline(txtlinbuf=>[f])
            cursvpos(f)
            return
        fin
    next
    bell
end
//
// Keyboard routines
//
def dev_control(devnum, code, list)#1
    byte params[5]

    params.0 = 3
    params.1 = devnum
    params.2 = code
    params:3 = list
    perr     = syscall($83, @params)
    return perr
end
def cons_keyavail
    byte params[5]
    byte count

    params.0 = 3
    params.1 = cmdsys.devcons
    params.2 = 5
    params:3 = @count
    return syscall($82, @params) ?? 0 :: count
end
def cons_keyread
    byte params[8]
    byte key

    params.0 = 4
    params.1 = cmdsys.refcons
    params:2 = @key
    params:4 = 1
    params:6 = 0
    syscall($CA, @params)
    return params:6 ?? key :: 0
end
def keyin3
    byte key

    repeat
        cursflash
    until cons_keyavail
    key = cons_keyread
    if key & $80 // Open Apple modifier
        when key
            is keyarrowleft
                key = keyctrla; break
            is keyarrowright
                key = keyctrls; break
            is keyarrowup
                key = keyctrlw; break
            is keyarrowdown
                key = keyctrlz; break
            is keyenter
                key = keyctrlf; break
            is $80 | '\\'
                key = keydelete; break // Delete
            //
            // Map OA+keypad
            //
            is $80 | '4'
                key = keyarrowleft; break
            is $80 | '6'
                key = keyarrowright; break
            is $80 | '8'
                key = keyarrowup; break
            is $80 | '2'
                key = keyarrowdown; break
            is $80 | '7'
                key = keyctrlq; break // Top
            is $80 | '1'
                key = keyctrle; break // Bottom
            is $80 | '9'
                key = keyctrlw; break // Pg Up
            is $80 | '3'
                key = keyctrlz; break // Pg Dn
            is $80 | '5'
                key = keyctrld; break // Del
            is $80 | '.'
                key = keyctrlc; break // Copy
            is $80 | '0'
                key = keyctrlv; break // Paste
            is $80 | '-'
                key = keyctrlx; break // Cut
        wend
    fin
    return key | $80
end
def keyin2e
    byte key
    repeat
        cursflash
        key = ^keyboard
    until key >= 128
    ^keystrobe
    if ^pushbttn2 & 128 // Closed Apple pressed
        when key
            is keyarrowleft
                key = keyctrla; break
            is keyarrowright
                key = keyctrls; break
            is keyarrowup
                key = keyctrlw; break
            is keyarrowdown
                key = keyctrlz; break
            is keyenter
                key = keyctrlf; break
        wend
    fin
    return key
end
def keyin2
    byte key

    repeat
        cursflash
        key = ^keyboard
        if key == keyctrll
            ^keystrobe
            flags = flags ^ shiftlock
            key   = 0
        fin
    until key >= 128
    ^keystrobe
    when key
        is keyctrln
            key = $DB // '['
            break
        is $9E // SHIFT+CTRL+N
            key = $FE // '~'
            break
        is keyctrlp
            key = $DC // '\'
            break
        is $80 // SHIFT+CTRL+P -> CTRL+@
            key = $FC // '|'
            break
        is keyctrlg
            key = $DF // '_'
            break
        is keyarrowleft
            if ^pushbttn3 < 128
                key = keydelete
            fin
            break
        otherwise
            if key >= $C0 and flags < shiftlock
                if ^pushbttn3 < 128
                    when key
                        is $C0
                            key = $D0 // P
                            break
                        is $DD
                            key = $CD // M
                            break
                        is $DE
                            key = $CE // N
                    wend
                else
                   key = key | $E0
                fin
            fin
    wend
    return key
end
def setkeyin#0
    when MACHID & MACHID_MODEL
        is MACHID_IIE
        is MACHID_IIC
            keyin = @keyin2e
            break
        is MACHID_III
            keyin = @keyin3
            break
        otherwise // ][ or ][+
            keyin = @keyin2
    wend
end
def tabkeyin
    return curscol < MAXLNLEN and curscol & $01 ?? keyspace :: 0
end
//
// Printer routines
//
def printtxt(slot)#0
    byte txtbuf[80]
    word i, scrncsw

    scrncsw = *csw
    *csw  = $C000 | (slot << 8)
    for i = 0 to numlines - 1
        lncpy(@txtbuf, txtlinbuf=>[i])
        puts(@txtbuf)
        putln
    next
    *csw = scrncsw
end
def openline(row)
    if numlines < MAXLINES
        memcpy(@txtlinbuf=>[row + 1], @txtlinbuf=>[row], (numlines - row) * 2)
        txtlinbuf=>[row] = @nullstr
        numlines++
        flags = flags | changed
        return TRUE
    fin
    bell
    return FALSE
end
def freesel#0
    word i
    
    for i = 0 to numcliplines - 1
        if cliplinbuf=>[i] <> @nullstr
            delstr(cliplinbuf=>[i])
            cliplinbuf=>[i] = @nullstr
        fin
    next
    numcliplines = 0
end
def selrange#2
    word first, last
    
    if flags & selection
        if cursrow > selrow
            first, last = selrow, cursrow
        else
            first, last = cursrow, selrow
        fin
    else
        first = cursrow
        last = first
    fin
    return first, last
end
def beginsel#0
    flags  = flags ^ selection
    selrow = cursrow
    drawgutter(scrntop, scrnleft)
end
def copysel#0
    word firstsel, lastsel
    
    freesel
    firstsel, lastsel = selrange
    for numcliplines = 0 to lastsel - firstsel
        cliplinbuf=>[numcliplines] = newstr(txtlinbuf=>[firstsel + numcliplines])
    next
    flags = flags & ~selection
    drawgutter(scrntop, scrnleft)
end
def cutsel#0
    word firstsel, lastsel
    
    freesel
    firstsel, lastsel = selrange
    if lastsel - firstsel < MAXCLIPLINES
        for numcliplines = 0 to lastsel - firstsel
            cliplinbuf=>[numcliplines] = txtlinbuf=>[firstsel + numcliplines]
        next
        memcpy(@txtlinbuf=>[firstsel], @txtlinbuf=>[lastsel + 1], (numlines - lastsel + 1) * 2)
        numlines = numlines - numcliplines
        cursrow  = firstsel
        if cursrow >= numlines
            cursrow = numlines - 1
        fin
        if cursrow < scrntop
            scrntop = cursrow
        fin
        cursy = cursrow - scrntop
        flags = flags | changed
        flags = flags & ~selection
        redraw
    else
        bell
    fin
end
def pastesel#0
    word p
    
    if numcliplines and numcliplines + numlines < MAXLINES
        memcpy(@txtlinbuf=>[cursrow + numcliplines], @txtlinbuf=>[cursrow], (numlines - cursrow) * 2)
        for p = 0 to numcliplines - 1
            txtlinbuf=>[cursrow + p] = newstr(cliplinbuf=>[p])
        next
        numlines = numlines + numcliplines
        flags = flags | changed
        redraw
    else
        bell
    fin
end
def indentsel#0
    byte indentstr[80], l
    word firstsel, lastsel, i
    
    freesel
    firstsel, lastsel = selrange
    for i = firstsel to lastsel
        l = ^(txtlinbuf=>[i])
        if l < MAXLNLEN - 2
            memcpy(@indentstr + 3, txtlinbuf=>[i] + 1, l)
            indentstr[0] = l + 2
            indentstr[1] = $A0
            indentstr[2] = $A0
            delstr(txtlinbuf=>[i])
            txtlinbuf=>[i] = newstr(@indentstr)
            flags = flags | changed
        fin
    next
    redraw
end
def undentsel#0
    byte undentstr[80], l
    word firstsel, lastsel, i
    
    freesel
    firstsel, lastsel = selrange
    for i = firstsel to lastsel
        l = ^(txtlinbuf=>[i])
        if l
            memcpy(@undentstr + 1, txtlinbuf=>[i] + 1, l)
            if undentstr[1] == $A0
                memcpy(@undentstr + 1, @undentstr + 2, l - 1)
                l--
                if l and undentstr[1] == $A0
                    memcpy(@undentstr + 1, @undentstr + 2, l - 1)
                    l--
                fin
                undentstr[0] = l
                delstr(txtlinbuf=>[i])
                txtlinbuf=>[i] = newstr(@undentstr)
                flags = flags | changed
            fin
        fin
    next
    redraw
end
def joinline#0
    byte joinstr[80], joinlen, stripjoin[80]

    if cursrow < numlines - 1
        strstripcpy(@joinstr, txtlinbuf=>[cursrow])
        memcpy(@stripjoin, txtlinbuf=>[cursrow + 1], ^(txtlinbuf=>[cursrow + 1]) + 1)
        striplead(@stripjoin, $80 | ' ');
        joinlen = joinstr + stripjoin
        if joinlen < 80
            curshpos(joinstr)
            memcpy(@joinstr + joinstr + 1, @stripjoin + 1, stripjoin)
            joinstr = joinlen
            delstr(txtlinbuf=>[cursrow])
            txtlinbuf=>[cursrow] = newstr(@joinstr)
            delstr(txtlinbuf=>[cursrow + 1])
            numlines--
            memcpy(@txtlinbuf=>[cursrow + 1], @txtlinbuf=>[cursrow + 2], (numlines - cursrow) * 2)
            flags = flags | changed
            redraw
        else
            bell
        fin
    fin
end
def splitline#0
    byte splitstr[80], splitlen, i

    if openline(cursrow + 1)
        if curscol
            splitlen = ^(txtlinbuf=>[cursrow])
            if curscol < splitlen - 1
                splitstr = splitlen - curscol
                memcpy(@splitstr + 1, txtlinbuf=>[cursrow] + curscol + 1, splitstr)
                striplead(@splitstr, $80 | ' ')
                for i = 1 to curscol
                    if ^(txtlinbuf=>[cursrow] + i) <> $80 | ' '
                        break
                    fin
                    memcpy(@splitstr + 2, @splitstr + 1, splitstr)
                    splitstr[1] = $80 | ' '
                    splitstr++
                next            
                txtlinbuf=>[cursrow + 1] = newstr(@splitstr)
                splitstr = curscol
                memcpy(@splitstr + 1, txtlinbuf=>[cursrow] + 1, splitstr)
                delstr(txtlinbuf=>[cursrow])
                txtlinbuf=>[cursrow] = newstr(@splitstr)
                curshpos(i - 1)
            else
                if splitlen > 0
                    for curscol = 1 to splitlen - 1
                        if ^(txtlinbuf=>[cursrow] + curscol) <> $80 | ' '
                            break
                        fin
                    next
                    curshpos(curscol - 1)            
                fin
            fin
         else
            txtlinbuf=>[cursrow + 1] = txtlinbuf=>[cursrow]
            txtlinbuf=>[cursrow]     = @nullstr
        fin
        redraw
        cursdown
    fin
end
def editkey(key)
    if key >= keyspace or key == keydelete or key == keyctrld or key == keyctrlr
        return TRUE
    fin
    return FALSE
end
def editline(key)
    byte editstr[80]
    word undoline

    if (editkey(key))
        flags = flags | changed
        memset(@editstr, $A0A0, 80)
        strstripcpy(@editstr, txtlinbuf=>[cursrow])
        undoline = txtlinbuf=>[cursrow]
        txtlinbuf=>[cursrow] = @editstr
        repeat
            if key >= keyspace
                if key == keydelete
                    if curscol > 0
                        if curscol <= editstr
                            memcpy(@editstr[curscol], @editstr[curscol + 1], editstr - curscol)
                            editstr--
                        fin
                        curshpos(curscol - 1)
                        curson
                    fin
                elsif curscol < MAXLNLEN
                    curshpos(curscol + 1)
                    if flags & insmode
                        if editstr < MAXLNLEN or editstr.MAXLNLEN == $A0
                            editstr++
                            if curscol >= editstr
                                editstr = curscol
                            else
                                memcpy(@editstr[curscol + 1], @editstr[curscol], editstr - curscol)
                            fin
                        else
                            curshpos(curscol - 1)
                            key = editstr[curscol]
                            bell
                        fin
                    else
                        if curscol > editstr
                            editstr = curscol
                        fin
                    fin
                    editstr[curscol] = caseconv(key)
                    cursoff
                    if cursx <= scrnwidth
                        drawrow(cursy, scrnleft, @editstr)
                    else
                        scrnleft++
                        cursx = scrnwidth
                        drawscrn(scrntop, scrnleft)
                    fin
                    curson
                else
                    bell
                fin
            elsif key == keyctrld
                if curscol < editstr
                    strstripcpy(undoline, @editstr)
                    memcpy(@editstr[curscol + 1], @editstr[curscol + 2], editstr - curscol)
                    editstr--
                    cursoff
                    drawrow(cursy, scrnleft, @editstr)
                    curson
                fin
            elsif key == keyctrlr
                strstripcpy(@editstr, undoline)
                cursoff
                drawrow(cursy, scrnleft, @editstr)
                curson
            fin
            key = keyin()
        until not editkey(key)
        if editstr
            txtlinbuf=>[cursrow] = newstr(@editstr)
        else
            txtlinbuf=>[cursrow] = @nullstr
        fin
        delstr(undoline)
    fin
    return key
end
def editmode#0
    if MACHID == $F2 // Apple 3
        dev_control(cmdsys.devcons, 11, @a3noecho)
    fin
    repeat
        when editline(keyin())
            is keyarrowup
                cursup; break
            is keyarrowdown
                cursdown; break
            is keyarrowleft
                cursleft; break
            is keyarrowright
                cursright; break
            is keyctrlw
                pgup; break
            is keyctrlz
                pgdown; break
            is keyctrla
                pgleft; break
            is keyctrls
                pgright; break
            is keyctrlq
                curshome; break
            is keyctrle
                cursend; break
            is keyctrlb
                beginsel; break
            is keyctrlc
                copysel; break
            is keyctrlx
                cutsel; break
            is keyctrlv
                pastesel; break
            is keyctrlf
                if cursrow == (numlines - 1)
                    if numlines < MAXLINES
                        numlines++
                        cursdown
                        flags = flags | changed
                        drawgutter(scrntop, scrnleft)
                    else
                        bell
                    fin
                    break
                fin
                cursdown
            is keyctrlo
                openline(cursrow)
                redraw
                break
            is keyenter
                if flags & insmode
                    splitline
                else
                    openline(cursrow + 1)
                    cursdown
                    redraw
                fin
                break
            is keyctrlt
                joinline; break
            is keyctrli
                if flags & selection
                    if flags & insmode
                        indentsel
                    else
                        undentsel
                    fin
                else
                    keyin = @tabkeyin
                    editline(keyspace)
                    setkeyin
                fin
                break
            is keyctrly
                if flags & insmode
                    flags = flags & ~insmode
                    curschr = ' '
                else
                    flags = flags | insmode
                    curschr = '+'
                fin
                break
            is keyescape
                if MACHID == $F2 // Apple 3
                    dev_control(cmdsys.devcons, 11, @a3echo)
                fin
                cursoff
                cmdmode
                if not exit
                    if MACHID == $F2 // Apple 3
                        dev_control(cmdsys.devcons, 11, @a3noecho)
                    fin
                    redraw
                fin
        wend
    until exit
end
//
// Command mode
//
def prfiles(optpath)
    byte path[64]
    byte refnum
    byte firstblk
    byte entrylen, entriesblk
    byte i, type, len
    word databuff, entry, filecnt

    if ^optpath
        strstripcpy(@path, optpath)
    else
        fileio:getpfx(@path)
        puts(@path)
        putln
    fin
    databuff = heapalloc(512)
    refnum = fileio:open(@path)
    if perr
        return perr
    fin
    firstblk = 1
    repeat
        if fileio:read(refnum, databuff, 512) == 512
            entry = databuff + 4
            if firstblk
                entrylen   = databuff->$23
                entriesblk = databuff->$24
                filecnt    = databuff=>$25
                entry      = entry + entrylen
            fin
            for i = firstblk to entriesblk
                type = ^entry
                if type <> 0
                    len = type & $0F
                    ^entry = len
                    puts(entry)
                    if type & $F0 == $D0 // Is it a directory?
                        putc('/')
                        len++
                    fin
                    for len = 20 - len downto 1
                        putc(' ')
                    next
                    filecnt--
                fin
                entry = entry + entrylen
            next
            firstblk = 0
        else
            filecnt = 0
        fin
    until filecnt == 0
    fileio:close(refnum)
    heaprelease(databuff)
    putln
    return 0
end
def parsecmd(strptr)
    byte cmd

    cmd = 0
    striplead(strptr, ' ')
    if ^strptr
        cmd = ^(strptr + 1)
        memcpy(strptr + 1, strptr + 2, ^strptr)
        ^strptr--
    fin
    if ^strptr
        striplead(strptr, ' ')
    fin
    return cmd
end
def chkchng
    if flags & changed
        puts("LOSE CHANGES TO FILE (Y/N)?")
        if toupper(keyin()) == 'N'
            putln
            return FALSE
        fin
        putln
    fin
    return TRUE
end
def cmdmode#0
    byte slot
    word cmdptr, line

    clrscrn
    puts("PLASMA Editor, Version 2.0 Dev\n")
    while not exit
        puts(@filename)
        cmdptr = gets($BA)
        when toupper(parsecmd(cmdptr))
            is 'F' // Find string
                if ^cmdptr
                    lnupcpy(@findstr, cmdptr)
                fin
                findtxt
                return
            is 'T' // Toggle upper/lower case display
                if flags & uppercase
                    txtlower
                else
                    txtupper
                fin
                return
            is 'G' // Goto line #
                line = strtonum(cmdptr)
                if line
                    curshpos(0)
                    cursvpos(line - 1)
               fin
            is 'E' // Edit mode
            is 0
                return
             is 'A' // Append file
                readtxt(cmdptr, numlines)
                flags = flags | changed
                break
            is 'R' // Read file
                if chkchng
                    inittxtbuf
                    strstripcpy(@filename, cmdptr)
                    readtxt(@filename, 0)
                    flags = flags & ~changed
                fin
                break
            is 'W' // Write file
                if ^cmdptr
                    strstripcpy(@filename, cmdptr)
                fin
                writetxt(@filename)
                //if flags & changed; fin
                flags = flags & ~changed
                break
            is 'C' // Catalog
                prfiles(cmdptr); break
            is 'P' // Prefix
                fileio:setpfx(cmdptr); break
            is 'H' // Hardcopy
                if ^cmdptr
                    slot = cmdptr.1 - '0'
                else
                    slot = 1
                fin
                printtxt(slot)
                break
            is 'Q' // Quit
                exit = chkchng
                if not exit
                    return
                fin
                break
           is 'N'
                if chkchng
                    inittxtbuf
                    strstripcpy(@filename, "UNTITLED")
                fin
                break
            otherwise
                bell
                putc('?')
                putln
        wend
        if perr
            puts("ERROR: $")
            putb(perr)
        else
            puts("OK")
        fin
        putln
    loop
end
//
// Init editor
//
setkeyin
if not (MACHID & $80) // ][ or ][+
    flags = uppercase | shiftlock
fin
inittxtbuf
arg = argNext(argFirst)
if ^arg
    strcpy(@filename, arg)
    puts(@filename)
    readtxt(@filename, 0)
fin
curschr  = '+'
flags    = flags | insmode
drawscrn(scrntop, scrnleft)
curson
editmode
done
